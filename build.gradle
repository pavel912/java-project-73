plugins {
	id 'java'
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'application'
	id 'checkstyle'
	id 'jacoco'
	id 'org.liquibase.gradle' version '2.0.4'
	id 'org.siouan.frontend-jdk11' version '6.0.0'
	id 'org.springdoc.openapi-gradle-plugin' version '1.3.3'
	id 'com.github.johnrengelman.processes' version '0.5.0'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation(
			// spring
			'org.springframework.boot:spring-boot-starter',
			'org.springframework.boot:spring-boot-starter-web',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.springframework.boot:spring-boot-starter-validation',
			'org.springframework.security:spring-security-core',
			'org.springframework.security:spring-security-config',
			'org.springframework.security:spring-security-web',
			'org.springframework.data:spring-data-commons',
			'org.springdoc:springdoc-openapi-ui:1.5.12',

			// jwt
			'io.jsonwebtoken:jjwt:0.9.1',

			// querydsl
			'com.querydsl:querydsl-core:5.0.0',
			'com.querydsl:querydsl-jpa:5.0.0',

			// rollbar
			'com.rollbar:rollbar-spring-boot3-webmvc:1.+',

			// liquibase
			'org.liquibase:liquibase-core:4.6.1',

			// apache
			'commons-io:commons-io:2.11.0',
			'org.apache.commons:commons-collections4:4.4'
	)

	liquibaseRuntime(
			'org.liquibase:liquibase-core:4.6.1',
			'org.springframework.boot:spring-boot-starter-data-jpa',
			'org.liquibase.ext:liquibase-hibernate5:4.5.0',
			"jakarta.xml.bind:jakarta.xml.bind-api:2.3.2",
			sourceSets.main.output
	)

	annotationProcessor(
			'org.projectlombok:lombok',
			'javax.annotation:javax.annotation-api:1.3.2',
			'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final',
			'com.querydsl:querydsl-apt:5.0.0:jpa'
	)

	runtimeOnly(
			'com.h2database:h2:1.4.200',
			'org.postgresql:postgresql:42.5.4'
	)

	testImplementation(
			'org.springframework.boot:spring-boot-starter-test',
			'org.projectlombok:lombok:1.18.22'
	)

	compileOnly 'org.projectlombok:lombok'
}

application {
	mainClass = 'hexlet.code.TaskManagerApplication'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}

	dependsOn test
}

diffChangeLog {
	dependsOn compileJava
}


liquibase {
	activities {
		main {
			changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
			url 'jdbc:h2:./taskManagerDb'
			username 'admin'
			password 'admin'
			referenceUrl 'hibernate:spring:hexlet.code.domain' +
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy'
		}
	}
}

openApi {
	apiDocsUrl.set("http://localhost:5000/v3/api-docs")
}

frontend {
	nodeVersion = '16.13.1'
	installScript = 'install'
	assembleScript = 'run build'
	packageJsonDirectory = file("${projectDir}/frontend")
}

def configFrontendTasks = {
	inputs.files(fileTree("$projectDir/frontend").exclude('build', 'node_modules'))
	outputs.dir("$buildDir/resources/main/static")
}

assembleFrontend {
	configure configFrontendTasks

	doLast {
		copy {
			from "$projectDir/frontend/build"
			into "$buildDir/resources/main/static"
		}
	}
}

installFrontend {
	configure configFrontendTasks
}

processResources.dependsOn assembleFrontend
